NOTE "CREATOR" "bbbb version 6.9";
NOTE "DATE" "1998/01/21";
NOTE "STAPL_VERSION" "JESD00-C";
NOTE "ALG_VERSION" "4";
NOTE "STACK_DEPTH" "3";
NOTE "MAX_FREQ" "10000000"; `10MHz
NOTE "TARGET" "1";
`*********************************************************
ACTION READ_IDCODE = DO_READ_IDCODE;
`*********************************************************
DATA data_id;
BOOLEAN idcode_data[32*10]; `[idcode_length * max_num_devices]
BOOLEAN i_idcode[10] = #1001101000; `assumed IDCODE instruction
BOOLEAN ones_data[10*32] =
$FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF;
BOOLEAN tmp_ir[10*10]; `[ir_length * max_num_devices]
BOOLEAN read_data[10+1]; `max_num_devices + 1
INTEGER max_num_devices =10;
INTEGER ir_length=10;
INTEGER idcode_length =32;
INTEGER i;
INTEGER j;
INTEGER number_of_chips;
ENDDATA;
`*****************************************************
PROCEDURE DO_READ_IDCODE USES compute_number_of_chips, data_id;
`Initialize devices
IRSTOP IRPAUSE;
DRSTOP DRPAUSE;
STATE RESET;
CALL compute_number_of_chips;
`Assume all devices have same idcode instruction bit pattern
FOR i=0 TO (number_of_chips-1);
FOR j=0 TO 9;
tmp_ir[(i* ir_length)+j] = i_idcode [j];
NEXT j;
NEXT i;
IRSCAN (number_of_chips*ir_length),
tmp_ir[((number_of_chips*ir_length)-1)..0];
DRSCAN (number_of_chips*idcode_length),
ones_data[((number_of_chips* idcode_length)-1)..0], CAPTURE
idcode_data[((number_of_chips* idcode_length)-1)..0];
FOR i=0 TO (number_of_chips-1);
EXPORT "IDCODE", idcode_data[(i*32)+31..(i*32)];
NEXT i;
ENDPROC;
`*********************************************************
PROCEDURE compute_number_of_chips USES data_id;
IRSCAN (ir_length * max_num_devices),
ones_data [((ir_length * max_num_devices)-1)..0];
DRSCAN (max_num_devices +1), ones_data [max_num_devices..0],
CAPTURE read_data[max_num_devices..0];
FOR i=0 TO max_num_devices-1;
IF(read_data[i] ==0) THEN
number_of_chips=number_of_chips+1;
NEXT i;
ENDPROC;
`********************************************************
CRC A3CA;
